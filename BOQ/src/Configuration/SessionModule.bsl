
Процедура УстановкаПараметровСеанса ( Параметры )
	
	Если ( Параметры = Неопределено ) Тогда
		начальноеЗаполнение ();
	Иначе
		Для Каждого параметр Из Параметры Цикл
			Если ( параметр = "ТекущийПользователь" ) Тогда
				установитьТекущегоПользователя ();
			ИначеЕсли ( параметр = "КодЯзыка" ) Тогда
				установитьКодЯзыка ();
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

Процедура начальноеЗаполнение ()
	
	Если ( НЕ ЗначениеЗаполнено ( ПараметрыСеанса.ТекущийПользователь ) ) Тогда
		установитьТекущегоПользователя ();	
	КонецЕсли;
	установитьКодЯзыка ();
	
КонецПроцедуры

Процедура установитьТекущегоПользователя ()
	
	пользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь ();
	Если ( пользовательИБ.имя = "" ) Тогда
		ВызватьИсключение Сообщения.ТекстПоИД ( "ПользовательИБНеОпределен" );
	Иначе
		с = "
		|выбрать Ссылка как Пользователь
		|из Справочник.Пользователи
		|где ИдентификаторПользователяИБ = &ИдентификаторПользователяИБ
		|";
		запрос = Новый Запрос ( с );
		запрос.УстановитьПараметр ( "ИдентификаторПользователяИБ", пользовательИБ.УникальныйИдентификатор );
		результат = запрос.Выполнить ();
		Если ( результат.Пустой () ) Тогда
			Если ( ДоступСервер.ПолныеПрава () ) Тогда
				ПараметрыСеанса.ТекущийПользователь = создатьПользователя ( пользовательИБ );	
			Иначе
				ВызватьИсключение Сообщения.ТекстПоИД ( "ОшибкаАвторизацииПользователя" );				
			КонецЕсли;	
		Иначе
			выборка = результат.Выбрать ();
			выборка.Следующий ();
			ПараметрыСеанса.ТекущийПользователь = выборка.Пользователь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура установитьКодЯзыка ()
	
	ПараметрыСеанса.КодЯзыка = ТекущийЯзык ().КодЯзыка;
	
КонецПроцедуры

Функция создатьПользователя ( ПользовательИБ )
	
	спрОбъект = Справочники.Пользователи.СоздатьЭлемент ();
	спрОбъект.ИдентификаторПользователяИБ = ПользовательИБ.УникальныйИдентификатор;
	спрОбъект.Наименование = ПользовательИБ.Имя;
	спрОбъект.Записать ();
	Возврат спрОбъект.Ссылка;

КонецФункции