
&НаСервере
Процедура ПриСозданииНаСервере ( Отказ, СтандартнаяОбработка )
	
	Если ( Объект.Ссылка.Пустая () ) Тогда
		начальноеЗаполнение ();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Процедура начальноеЗаполнение ()
	
	Объект.Администратор = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьПользователя ( Команда )
	
	п = Новый Структура;
	п.Вставить ( "РежимВыбора", Истина );
	п.Вставить ( "ЗакрыватьПриВыборе", Ложь );
	ОткрытьФорму ( "Справочник.Пользователи.Форма.Выбор", п, Элементы.Пользователи );
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиОбработкаВыбора ( Элемент, ВыбранноеЗначение, СтандартнаяОбработка )
	
	Если ТипЗнч ( ВыбранноеЗначение ) = Тип ( "Массив" ) Тогда
		Для Каждого Значение Из ВыбранноеЗначение Цикл
			обработкаВыбораПользователя ( Значение );
		КонецЦикла;
	Иначе
		обработкаВыбораПользователя ( ВыбранноеЗначение );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура обработкаВыбораПользователя ( ВыбранноеЗначение )
	
	Если ТипЗнч ( ВыбранноеЗначение ) = Тип ( "СправочникСсылка.Пользователи" ) Тогда
		Если Объект.Пользователи.НайтиСтроки ( Новый Структура ( "Пользователь", ВыбранноеЗначение ) ).Количество () = 0 Тогда
			Объект.Пользователи.Добавить ().Пользователь = ВыбранноеЗначение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи ( ПараметрыЗаписи )
	
	Оповестить ( "ЗаписьГруппыДоступа", Новый Структура (), Объект.Ссылка );
	
КонецПроцедуры
