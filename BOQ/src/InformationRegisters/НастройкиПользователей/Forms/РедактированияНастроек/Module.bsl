
&НаКлиенте
Процедура СохранитьНастройки ( Команда )
	
	ЗаписатьНастройкиПользователя ();	
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьНастройкиПользователя ()
	
	стр = "
	|ВЫБРАТЬ
	|	НастройкиПользователей.Родитель,
	|	НастройкиПользователей.Ссылка,
	|	НастройкиПользователей.ЭтоГруппа КАК ЭтоГруппа,
	|	ЗначенияНастроекПользователя.Значение КАК Значение
	|ИЗ
	|	ПланВидовХарактеристик.НастройкиПользователей КАК НастройкиПользователей
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПользователей КАК ЗначенияНастроекПользователя
	|		ПО НастройкиПользователей.Ссылка = ЗначенияНастроекПользователя.Настройка
	|			И ЗначенияНастроекПользователя.Пользователь = &Пользователь 
	|ГДЕ
	|	НЕ НастройкиПользователей.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоГруппа ИЕРАРХИЯ,
	|	НастройкиПользователей.Наименование
	|";
	запрос = Новый Запрос ( стр );
	запрос.УстановитьПараметр ( "Пользователь", Пользователь );
	результат = запрос.Выполнить ();
	выборка   = результат.Выбрать ();
	
	ДеревоЗначенийОбъект = Новый ДеревоЗначений;
	ДеревоЗначенийОбъект.Колонки.Добавить ( "Настройка", Новый ОписаниеТипов ( "ПланВидовХарактеристикСсылка.НастройкиПользователей" ) );
	ДеревоЗначенийОбъект.Колонки.Добавить ( "Значение", Метаданные.ПланыВидовХарактеристик.НастройкиПользователей.Тип );
	ДеревоЗначенийОбъект.Колонки.Добавить ( "ЭтоНастройка", Новый ОписаниеТипов ( "Булево" ) );
	
	Пока Выборка.Следующий () Цикл
		СтрокаНастройки = ДеревоЗначенийОбъект.Строки.Найти ( Выборка.Ссылка, "Настройка", ИСТИНА );
		Если ( СтрокаНастройки = Неопределено ) Тогда
			Если Выборка.Родитель.Пустая () Тогда
				СтрокаГруппы = ДеревоЗначенийОбъект;
			Иначе
				СтрокаГруппы = ДеревоЗначенийОбъект.Строки.Найти ( Выборка.Родитель, "Настройка", ИСТИНА );
				Если СтрокаГруппы = Неопределено Тогда
					СтрокаГруппы = ДеревоЗначенийОбъект.Строки.Добавить ();
					СтрокаГруппы.Настройка = Выборка.Родитель;
					СтрокаГруппы.ЭтоНастройка = ЛОЖЬ;
				КонецЕсли;
			КонецЕсли;		
				СтрокаНастройки = СтрокаГруппы.Строки.Добавить ();
				СтрокаНастройки.Настройка = Выборка.Ссылка;
				СтрокаНастройки.ЭтоНастройка = НЕ Выборка.ЭтоГруппа;
		КонецЕсли;
		ЗначениеНастройки = Выборка.Ссылка.ТипЗначения.ПривестиЗначение ( Выборка.Значение );
		СтрокаНастройки.Значение = ЗначениеНастройки;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы ( ДеревоЗначенийОбъект, "ДеревоНастроек");
	 
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные ( Команда )
	
	ЗаполнитьНастройкиПользователя ();	
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиПользователя ()
	
	ДеревоЗначенийОбъект = РеквизитФормыВЗначение ( "ДеревоНастроек" );
	СтрокиНастроек = ДеревоЗначенийОбъект.Строки.НайтиСтроки ( Новый Структура ( "ЭтоНастройка", ИСТИНА ), ИСТИНА );

	НаборЗаписей = РегистрыСведений.НастройкиПользователей.СоздатьНаборЗаписей ();
	НаборЗаписей.Отбор.Пользователь.Установить ( Пользователь );
	
	Для Каждого СтрокаНастроек Из СтрокиНастроек Цикл
		Запись = НаборЗаписей.Добавить ();
		Запись.Пользователь = Пользователь;
		Запись.Настройка = СтрокаНастроек.Настройка;
		Запись.Значение = СтрокаНастроек.Настройка.ТипЗначения.ПривестиЗначение ( СтрокаНастроек.Значение );		
	КонецЦикла; 
	НаборЗаписей.Записать ();
 	
КонецПроцедуры 

&НаКлиенте
Процедура РазвернутьДеревоНастроек ()
	
	КоллекцияЭлементовДереваНастроек = ПолучитьКоллекциюЭлементов ();
	Для Каждого СтрокаДереваНастроек Из КоллекцияЭлементовДереваНастроек Цикл
		ИдентификаторСтроки = СтрокаДереваНастроек.ПолучитьИдентификатор ();
		Элементы.ДеревоНастроек.Развернуть ( ИдентификаторСтроки );
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКоллекциюЭлементов ()
	
	Возврат ДеревоНастроек.ПолучитьЭлементы ();
	
КонецФункции 

&НаСервере
Процедура ПриСозданииНаСервере ( Отказ, СтандартнаяОбработка )
	
	Если ( ЗначениеЗаполнено ( Параметры.Пользователь ) ) Тогда
		Пользователь = Параметры.Пользователь;
	Иначе
		Отказ = ИСТИНА;
	КонецЕсли;
	ЗаполнитьНастройкиПользователя ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии ()
	
	ЗаписатьНастройкиПользователя ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии ( Отказ )
	
	РазвернутьДеревоНастроек ();
	
КонецПроцедуры