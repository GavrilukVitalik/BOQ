
Процедура ПрисоединитьФайл ( Объект, Файл ) Экспорт
	
	скопироватьФайл ( Объект, Файл );
	добавитьЗапись ( Объект, Файл );
	
КонецПроцедуры

Процедура скопироватьФайл ( Объект, Файл )
	
	слеш = РаботаСФайлами.РазделительОС ();
	папка = РаботаСФайлами.ПолучитьПапкуПоОбъекту ( Объект );
	путь = Константы.ХранениеФайлов.Получить () + слеш + папка + слеш;
	данные = ПолучитьИзВременногоХранилища ( Файл.Адрес );
	создатьПапкуОбъекта ( путь);
 	данные.Записать ( путь + Файл.Имя );
	Если ( ЭтоАдресВременногоХранилища ( Файл.Адрес ) ) Тогда
		УдалитьИзВременногоХранилища ( Файл.Адрес ); 
	КонецЕсли;	
	
КонецПроцедуры

Процедура добавитьЗапись ( Объект, Файл )
	
	имяФайла = СтрЗаменить ( Файл.Имя, Файл.Расширение, "" );
	расширение = СтрЗаменить ( Файл.Расширение, ".", "" );
	набор = РегистрыСведений.Файлы.СоздатьНаборЗаписей ();
	набор.Отбор.Объект.Установить ( Объект );
	набор.Отбор.ИмяФайла.Установить ( имяФайла );
	набор.Отбор.Расширение.Установить ( расширение );
	набор.Прочитать ();
	Если ( набор.Количество () = 0 ) Тогда
		запись = набор.Добавить ();
	Иначе
		запись = набор [ 0 ];
	КонецЕсли;
	запись.Объект = Объект;
	запись.ИмяФайла = имяФайла;
	запись.Расширение = расширение;
	запись.Дата = ТекущаяДата ();
	запись.Размер = Файл.Размер / 1024;
	запись.Автор = ПараметрыСеанса.ТекущийПользователь;
	запись.Папка = РаботаСФайлами.ПолучитьПапкуПоОбъекту ( Объект );
	запись.Дата = ТекущаяДата ();
	набор.Записать ();
	
КонецПроцедуры

Процедура создатьПапкуОбъекта ( ХранениеФайлов )
	
	путьКаталога = Лев ( ХранениеФайлов, СтрДлина ( ХранениеФайлов ) - 1 );
	каталог = Новый Файл ( путьКаталога );
	Если ( каталог.Существует () ) Тогда
		// код ...  
	Иначе
		СоздатьКаталог ( каталог.ПолноеИмя );
	КонецЕсли;
	
КонецПроцедуры

Функция ПоместитьФайлВХранилище ( Объект, ИмяФайла, Расширение ) Экспорт
	
	с = "
	|выбрать
	|	Объект как Объект, ИмяФайла как ИмяФайла, Расширение как Расширение,
	|	Дата как Дата, Размер как Размер, Автор как Автор, Папка как Папка,
	|	Значение как ХранениеФайлов
	|из РегистрСведений.Файлы,
	|	Константа.ХранениеФайлов
	|где Объект = &Объект и ИмяФайла = &ИмяФайла и Расширение = &Расширение
	|";
	запрос = Новый Запрос ( с );
	запрос.УстановитьПараметр ( "Объект", Объект );
	запрос.УстановитьПараметр ( "ИмяФайла", ИмяФайла );
	запрос.УстановитьПараметр ( "Расширение", Расширение );
	результат = запрос.Выполнить ();
	Если ( результат.Пустой () ) Тогда
		Возврат Неопределено;	
	КонецЕсли;
	выборка = результат.Выбрать ();
	выборка.Следующий ();
	слеш = РаботаСФайлами.РазделительОС ();
	путь = выборка.ХранениеФайлов + слеш + выборка.Папка + слеш + ИмяФайла + "." + Расширение;	
	данные = Новый ДвоичныеДанные ( путь );
	Возврат ПоместитьВоВременноеХранилище ( данные );

КонецФункции

Процедура УдалитьЗапись ( Объект, ИмяФайла, Расширение ) Экспорт
	
	набор = РегистрыСведений.Файлы.СоздатьНаборЗаписей ();
	набор.Отбор.Объект.Установить ( Объект );
	набор.Отбор.ИмяФайла.Установить ( ИмяФайла );
	набор.Отбор.Расширение.Установить ( Расширение );
	набор.Прочитать ();
	набор.Очистить ();
	набор.Записать ();
	
КонецПроцедуры

Функция ПолучитьПапкуХранения ( Объект ) Экспорт
	
	слеш = РаботаСФайлами.РазделительОС ();
	папка = Константы.ХранениеФайлов.Получить () + слеш + РаботаСФайлами.ПолучитьПапкуПоОбъекту ( Объект ) + слеш;
	Возврат папка;

КонецФункции