	  
Функция ПолучитьРеквизиты ( Знач Ссылка, Знач Поля ) Экспорт
	
	Если ( ТипЗнч ( Поля ) = Тип ( "Массив" ) ) Тогда
		с = СтрСоединить ( Поля, ", " );
	Иначе
		с = Поля;
	КонецЕсли;
	мета = Ссылка.Метаданные ();
	типы = получитьТипы ( мета, с );
	с = "
	|выбрать разрешенные 
	|	" + с + "
	|из " + мета.ПолноеИмя () + " как Таблица
	|где Таблица.Ссылка = &Ссылка
	|";
	запрос = Новый Запрос ( с );
	запрос.УстановитьПараметр ( "Ссылка", Ссылка );
	результат = запрос.Выполнить ();
	выборка = результат.Выбрать ();
	выборка.Следующий ();
	данные = Новый Структура ();
	Для Каждого колонка Из результат.Колонки Цикл
		имя = колонка.Имя;
		типЗначения = типы [ имя ];
		значение = выборка [ имя ];
		Если ( типЗначения <> Неопределено ) Тогда
			значение = типЗначения.ПривестиЗначение ( значение );
		КонецЕсли;
		данные.Вставить ( имя, значение );
	КонецЦикла;
	Возврат данные;

КонецФункции

Функция получитьТипы ( Мета, Поля )
	
	Перем поле;
	Перем имя;
	типы = Новый Структура ();
	м = Преобразования.СтрокаВМассив ( Поля );
	Для Каждого реквизит Из м Цикл
		установитьПолеИИмя ( реквизит, поле, имя );
		типы.Вставить ( имя, получитьТипПоля ( мета, поле ) );
	КонецЦикла;
	Возврат типы;

КонецФункции

Процедура установитьПолеИИмя ( Реквизит, Поле, Имя )
	
	синоним = СтрНайти ( Реквизит, " КАК " );
	Если ( синоним = 0 ) Тогда
		синоним = СтрНайти ( Реквизит, " как " );		
	КонецЕсли;
	Если ( синоним = 0 ) Тогда
		Имя = СтрЗаменить ( Реквизит, ".", "" );
		Поле = Реквизит;	
	Иначе
		Имя = Сред ( Реквизит, ( синоним + 4 ) );
		Поле = Лев ( Реквизит, ( синоним - 1 ) );
	КонецЕсли;

КонецПроцедуры

Функция получитьТипПоля ( Мета, Поле )
	
	текущиеМета = Мета;
	реквизиты = Преобразования.СтрокаВМассив ( Поле, "." );
	Для Каждого реквизит Из реквизиты Цикл
		поискРеквизит = текущиеМета.Реквизиты.Найти ( реквизит );
		Если ( поискРеквизит = Неопределено ) Тогда
			поискРеквизит = текущиеМета.СтандартныеРеквизиты [ реквизит ];
		КонецЕсли;
		текущийТип = поискРеквизит.Тип;
		текущиеТипы = текущийТип.Типы ();
		Если ( текущийТип = Тип ( "Дата" ) Или текущийТип = Тип ( "Строка" ) 
			Или текущийТип = Тип ( "Число" ) Или текущийТип = Тип ( "Булево" ) 
			Или текущийТип = Тип ( "ХранилищеЗначения" ) ) Тогда
			Прервать;
		КонецЕсли;
		Если ( текущиеТипы.Количество () > 1 ) Тогда
			текущийТип = Неопределено;
			Прервать;
		КонецЕсли;
		текущиеМета = Метаданные.НайтиПоТипу ( текущиеТипы [ 0 ] );
	КонецЦикла;
	Возврат текущийТип;

КонецФункции