&НаКлиенте
Перем Расписание;

&НаКлиенте
Процедура ОК(Команда)
	
	Если ЗаписатьРегламентноеЗадание(Расписание) Тогда
		Закрыть(РегламентноеЗаданиеИД);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРасписаниеНажатие ( Элемент )
	
	#Если ( ВебКлиент Или ТонкийКлиент Или ТолстыйКлиентУправляемоеПриложение ) Тогда
		диалог = Новый ДиалогРасписанияРегламентногоЗадания  ( Расписание );
		диалог.Показать ( Новый ОписаниеОповещения ( "ВыбратьРасписание", ЭтотОбъект ) );
	#КонецЕсли
	
КонецПроцедуры

&AtClient
Procedure ВыбратьРасписание ( Ответ, Парамы ) Экспорт
	
	Если ( Ответ = Неопределено ) Тогда
		Возврат;
	КонецЕсли; 
	Расписание = Ответ;
	Элементы.НадписьРасписание.Заголовок = "Выполнять: " + Строка ( Расписание );
	
EndProcedure

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого Метаданное из Метаданные.РегламентныеЗадания Цикл
		Элементы.МетаданныеВыбор.СписокВыбора.Добавить(Метаданное.Имя, Метаданное.Представление());
	КонецЦикла;
	мВозможностьИзменятьМетаданные = Истина;
	РегламентноеЗаданиеИД = Параметры.ИдентификаторЗадания;
	РегламентноеЗадание = Обработки.КонсольЗаданий.Создать().ПолучитьОбъектРегламентногоЗадания(РегламентноеЗаданиеИД);
	Если РегламентноеЗадание <> Неопределено Тогда
		МетаданныеВыбор = РегламентноеЗадание.Метаданные.Имя;
		мВозможностьИзменятьМетаданные = Ложь;
		Наименование = РегламентноеЗадание.Наименование;
		Ключ = РегламентноеЗадание.Ключ;
		Использование = РегламентноеЗадание.Использование;
		ПользователиВыбор = РегламентноеЗадание.ИмяПользователя;
		КоличествоПовторовПриАварийномЗавершении = РегламентноеЗадание.КоличествоПовторовПриАварийномЗавершении;
		ИнтервалПовтораПриАварийномЗавершении = РегламентноеЗадание.ИнтервалПовтораПриАварийномЗавершении;
		Расписание = РегламентноеЗадание.Расписание;
	Иначе
		Расписание = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	Элементы.МетаданныеВыбор.Доступность = мВозможностьИзменятьМетаданные;
	Элементы.НадписьРасписание.Заголовок = Сообщения.ТекстПоИД ( "Выполнять" ) + Строка(Расписание);

КонецПроцедуры

&НаСервере
Функция ЗаписатьРегламентноеЗадание(Расписание)
	
	Попытка
		Если МетаданныеВыбор = Неопределено ИЛИ МетаданныеВыбор = "" Тогда
			ВызватьИсключение Сообщения.ТекстПоИД ( "НеВыбраныМетаданныеРегламентногоЗадания" );
		КонецЕсли;
		РегламентноеЗадание = Обработки.КонсольЗаданий.Создать().ПолучитьОбъектРегламентногоЗадания(РегламентноеЗаданиеИД);
		Если РегламентноеЗадание = Неопределено Тогда
			РегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(МетаданныеВыбор);
			РегламентноеЗаданиеИД = РегламентноеЗадание.УникальныйИдентификатор;
		КонецЕсли;
		РегламентноеЗадание.Наименование = Наименование;
		РегламентноеЗадание.Ключ = Ключ;
		РегламентноеЗадание.Использование = Использование;
		РегламентноеЗадание.ИмяПользователя = ПользователиВыбор;
		РегламентноеЗадание.КоличествоПовторовПриАварийномЗавершении = КоличествоПовторовПриАварийномЗавершении;
		РегламентноеЗадание.ИнтервалПовтораПриАварийномЗавершении = ИнтервалПовтораПриАварийномЗавершении;
		РегламентноеЗадание.Расписание = Расписание;
		РегламентноеЗадание.Записать();
	Исключение	
		Сообщения.СообщениеВывести ( "ОписаниеОшибки", Новый Структура ( "Ошибка", ОписаниеОшибки () ) );
		Возврат Ложь;
	КонецПопытки;
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Расписание = ПолучитьРасписаниеРегламентногоЗадания(РегламентноеЗаданиеИД);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРасписаниеРегламентногоЗадания(УникальныйНомерЗадания) export
	
	ОбъектЗадания = Обработки.КонсольЗаданий.Создать().ПолучитьОбъектРегламентногоЗадания(УникальныйНомерЗадания);
	Если ОбъектЗадания = Неопределено Тогда
		Возврат Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	Возврат ОбъектЗадания.Расписание;
	
КонецФункции